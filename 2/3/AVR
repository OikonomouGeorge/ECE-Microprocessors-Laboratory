.include "m328PBdef.inc"
    
.equ FOSC_MHZ = 16
    
.equ DEL_mS = 4000		; Delay in mS (<= 4095)
.equ DEL_NU = FOSC_MHZ * DEL_mS ; Routine: 1000 * DEL_NU + 6 Cycles

.equ DEL_In_mS = 500
.equ DEL_In_NU = FOSC_MHZ * DEL_In_mS
				
.equ DEL_Control_mS = 5   
.equ DEL_Control_NU = FOSC_MHZ * DEL_Control_mS 
    
.def light = r21		; Used to indicate that the LED was on 
				; during the previous iteration
.def temp = r22

.org 0x0
rjmp reset

.org 0x4
rjmp isr1
    
reset:
    ; Init SP
    ldi r24, LOW(RAMEND)
    out SPL, r24
    ldi r24, HIGH(RAMEND)
    out SPH, r24
    
    ser temp
    out DDRB, temp ; Init PORTB as Output  
    clr temp
    out DDRD, temp ; Init PORTD as Input

    ; Interrupt on Rising Edge of INT1 Pin
    ldi temp, (1<<ISC11) | (1<<ISC10)
    sts EICRA, temp

    ; Enable the INT1 Interrupt
    ldi temp, (1<<INT1)
    out EIMSK, temp

    ; Enable General Flag of Interrupts
    sei
    
    ; Clear Pins of PORTB
    clr temp
    out PORTB, temp
    
    ; Init Led Off
    clr light

main:
    rjmp main
    
; External Interrupt 1 Service Routine
isr1:
    ;	push r21	
    push r22
    push r23
    push r24
    push r25
    in r25, SREG
    push r25            ; Save r23, r24, 25, SREG to Stack
    
control:
    ; Debouncing
    ; Clear External Interrupt 1 Flag
    ldi r24, (1 << INTF1)
    out EIFR, r24    
    ldi r24, low(DEL_Control_NU)
    ldi r25, high(DEL_Control_NU) 
    rcall delay_mS
    sbic EIFR, INTF1    ; Tests if INTF1 = 0
    rjmp exit		; Skips if INTF1 = 0    
    
    cpi light, 0xFF     ; Compare Reg with Immediate
    breq renewal	; If Led is On, to renewal
    			
turn_light_on:    
    ldi light, 0xFF	; Set Led On
    ldi temp, 0x01
    out PORTB, temp	; Turn PB0 On
    
    ; Enable General Flag of Interrupts
    sei
    
    ldi r24, low(DEL_NU)
    ldi r25, high(DEL_NU)
    rcall delay_mS
    
turn_light_off:
    ldi light, 0x00	; Set Led Off
    ldi temp, 0x00
    out PORTB, temp	; Turn PB0 Off
    rjmp exit
    
; If Led is On
renewal:
    ldi temp, 0xFF	; Turn all Leds On
    out PORTB, temp
    
    ldi r24, low(DEL_In_NU)
    ldi r25, high(DEL_In_NU)
    rcall delay_mS
    ;ldi temp, 0x01	; Turn Led On
    ;out PORTB, temp	; Turn PB0 On

    ;ldi light, 0xFF	; Set Led On
    rjmp turn_light_on
    
delay_mS:
    ldi r23, 249
loop_inn:
    dec r23
    nop
    brne loop_inn
    sbiw r24, 1
    brne delay_mS
    ret   

exit:   
 ; Clear All Pins of PORTB
 clr temp
 out PORTB, temp
 
 ; Clear External Interrupt 1 Flag
 ldi temp, (1 << INTF1)
 out EIFR, temp 

 pop r25
 out SREG, r25
 pop r25
 pop r24
 pop r23	    ; Retrieve r23, r24, 25, SREG from Stack
 pop r22
 ;  pop r21
 reti		    ; Return from interrupt
